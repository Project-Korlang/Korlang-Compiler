fun Parser_parse_item(self: Parser) -> Result<Item, Bool> {
  let is_pub = self.match_keyword("pub");
  
  if (self.match_keyword("@nogc")) {
    self.expect_keyword("fun")?;
    return Result.ok(Item.Fun(self.parse_fun(is_pub, true)?));
  }
  if (self.match_keyword("gpu")) {
    self.expect_keyword("fun")?;
    return Result.ok(Item.Fun(self.parse_fun(is_pub, false)?));
  }
  if (self.match_keyword("fun")) { return Result.ok(Item.Fun(self.parse_fun(is_pub, false)?)); }
  if (self.match_keyword("struct")) { return Result.ok(Item.Struct(self.parse_struct(is_pub)?)); }
  if (self.match_keyword("enum")) { return Result.ok(Item.Enum(self.parse_enum(is_pub)?)); }
  if (self.match_keyword("type")) { return Result.ok(Item.TypeAlias(self.parse_type_alias(is_pub)?)); }
  if (self.match_keyword("view")) { return Result.ok(Item.View(self.parse_view(is_pub)?)); }
  if (self.match_keyword("resource")) { return Result.ok(Item.Resource(self.parse_resource(is_pub)?)); }
  if (self.check_keyword("let") || self.check_keyword("var")) {
    return Result.ok(Item.Const(self.parse_var_decl_full(is_pub)?));
  }
  Result.ok(Item.Stmt(self.parse_stmt()?))
}

fun Parser_parse_fun(self: Parser, is_pub: Bool, nogc: Bool) -> Result<FunDecl, Bool> {
  let start = self.prev_span();
  let name = self.expect_ident()?;
  let params = self.parse_param_list()?;
  let ret = if (self.match_kind(TokenKind.Arrow)) { self.parse_type_ref()? } else { null };
  let body = self.parse_block()?;
  let end = body.span.end;
  Result.ok(FunDecl { is_pub: is_pub, name: name, params: params, ret: ret, body: body, nogc: nogc, span: Span_new(start.start, end) })
}

fun Parser_parse_struct(self: Parser, is_pub: Bool) -> Result<StructDecl, Bool> {
  let start = self.prev_span();
  let name = self.expect_ident()?;
  self.expect_kind(TokenKind.LBrace)?;
  let fields = List.new();
  while (!self.check_kind(TokenKind.RBrace) && !self.at_eof()) {
    let field_name = self.expect_ident()?;
    self.expect_kind(TokenKind.Colon)?;
    let ty = self.parse_type_ref()?;
    let semi = self.expect_kind(TokenKind.Semi)?;
    fields.push(FieldDecl { name: field_name, ty: ty, span: semi.span });
  }
  let end = self.expect_kind(TokenKind.RBrace)?;
  Result.ok(StructDecl { is_pub: is_pub, name: name, fields: fields, span: Span_new(start.start, end.span.end) })
}

fun Parser_parse_enum(self: Parser, is_pub: Bool) -> Result<EnumDecl, Bool> {
  let start = self.prev_span();
  let name = self.expect_ident()?;
  self.expect_kind(TokenKind.LBrace)?;
  let variants = List.new();
  while (!self.check_kind(TokenKind.RBrace) && !self.at_eof()) {
    let vname = self.expect_ident()?;
    let payload = List.new();
    if (self.match_kind(TokenKind.LParen)) {
      if (!self.check_kind(TokenKind.RParen)) {
        payload.push(self.parse_type_ref()?);
        while (self.match_kind(TokenKind.Comma)) { payload.push(self.parse_type_ref()?); }
      }
      self.expect_kind(TokenKind.RParen)?;
    }
    let semi = self.expect_kind(TokenKind.Semi)?;
    variants.push(VariantDecl { name: vname, payload: payload, span: semi.span });
  }
  let end = self.expect_kind(TokenKind.RBrace)?;
  Result.ok(EnumDecl { is_pub: is_pub, name: name, variants: variants, span: Span_new(start.start, end.span.end) })
}

fun Parser_parse_type_alias(self: Parser, is_pub: Bool) -> Result<TypeAliasDecl, Bool> {
  let start = self.prev_span();
  let name = self.expect_ident()?;
  self.expect_kind(TokenKind.Eq)?;
  let target = self.parse_type_ref()?;
  let end = self.expect_kind(TokenKind.Semi)?;
  Result.ok(TypeAliasDecl { is_pub: is_pub, name: name, target: target, span: Span_new(start.start, end.span.end) })
}

fun Parser_parse_view(self: Parser, is_pub: Bool) -> Result<ViewDecl, Bool> {
  let start = self.prev_span();
  let name = self.expect_ident()?;
  let params = self.parse_param_list()?;
  let body = self.parse_view_block()?;
  let end = if (body.len() > 0) { body[body.len() - 1].span } else { start };
  Result.ok(ViewDecl { is_pub: is_pub, name: name, params: params, body: body, span: Span_new(start.start, end.end) })
}

fun Parser_parse_resource(self: Parser, is_pub: Bool) -> Result<ResourceDecl, Bool> {
  let start = self.prev_span();
  let name = self.expect_ident()?;
  self.expect_kind(TokenKind.LParen)?;
  let resource_type = self.parse_qualified_ident()?;
  self.expect_kind(TokenKind.RParen)?;
  self.expect_kind(TokenKind.LBrace)?;
  let entries = List.new();
  while (!self.check_kind(TokenKind.RBrace) && !self.at_eof()) {
    let key = self.expect_ident()?;
    self.expect_kind(TokenKind.Colon)?;
    let value = self.parse_expr()?;
    let semi = self.expect_kind(TokenKind.Semi)?;
    entries.push(ResourceEntry { key: key, value: value, span: semi.span });
  }
  let end = self.expect_kind(TokenKind.RBrace)?;
  Result.ok(ResourceDecl { is_pub: is_pub, name: name, resource_type: resource_type, entries: entries, span: Span_new(start.start, end.span.end) })
}

fun Parser_parse_var_decl_full(self: Parser, is_pub: Bool) -> Result<VarDecl, Bool> {
  let start = self.current_span();
  let immutable = self.match_keyword("let");
  if (!immutable && !self.match_keyword("var")) {
    self.error("expected 'let' or 'var'");
    return Result.err(false);
  }
  let decl = self.parse_var_decl_with(immutable)?;
  decl.is_pub = is_pub;
  Result.ok(decl)
}
